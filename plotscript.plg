set datafile separator ","
set key center top
set term pdf enhanced size 20cm,20cm color solid font 'Inconsolata,12';
set out plotFile;
set multiplot layout 3,2 title date font ",16"
set xtics rotate
set tics scale 0
set xrange [0:]
set yrange [0:]

set title "Vehicle Speed"; set xlabel "time (s)"; set ylabel "speed (mph)"
unset key
set yrange [0:90]
plot logFile u 1:2 w l

set title "Engine Speed"; set xlabel "time (s)"; set ylabel "speed (rpm)"
unset key
set yrange [0:5000]
plot logFile u 1:3 w l

set title "Coolant and Intake Temps"; set xlabel "time (s)"; set ylabel "temperature (F)"
set key right bottom
set yrange [0:200]
plot logFile u 1:4 w l t 'coolant', logFile u 1:7 w l t 'intake'

set title "Mass Air Flow"; set xlabel "time (s)"; set ylabel "air flow (g/s)"
unset key
set yrange [0:60]
plot logFile u 1:5 w l

set title "Engine Load"; set xlabel "time (s)"; set ylabel "load (percentage)"
unset key
set yrange [0:100]
plot logFile u 1:6 w l

set title "Instant Gas Mileage"; set xlabel "time (s)"; set ylabel "mileage (mpg)"
unset key
set yrange [0:110]
plot logFile u 1:($6 == 0 ? 110 : $5 == 0 ? NaN : 13 * $2 / $5) w l

reset
unset multiplot